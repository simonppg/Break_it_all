plugins {
    id 'com.android.application'
}
android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.example.game"
        minSdkVersion 28
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    testImplementation 'junit:junit:4.12'
}

task runApp(type: Exec, dependsOn: 'installDebug') {
    // linux
    commandLine 'adb', 'shell', 'am', 'start', '-n', 'com.example.game/.MainScreen'

    // windows
    // commandLine 'cmd', '/c', 'adb', 'shell', 'am', 'start', '-n', 'com.example.game/.MainScreen'
}

task clearLogcat(type: Exec) {
    // linux
    commandLine 'adb', 'shell', 'logcat',  '-c'
}

task run(type: Exec, dependsOn: 'clearLogcat') {
    // linux
    dependsOn 'runApp'
    dependsOn 'clearLogcat'
    tasks.findByName('runApp').mustRunAfter 'clearLogcat'

    commandLine 'adb', 'shell', 'logcat',  'libNative:I', '*:S'
}
